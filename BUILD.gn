declare_args() {
  # Storm can be built as a header only library or a separate compilation.
  # Enabling this adds the STORM_SEPARATE_COMPILATION define.
  storm_header_only = true
}

config("storm_config") {
  include_dirs = [ "include" ]
}

config("storm_lib_config") {
  defines = [ "STORM_SEPARATE_COMPILATION" ]
}

_storm_deps = [
  "$external/asioext",
  "$external/boost/libs/filesystem",
  "$external/boost/libs/system",
  "$external/fmt",
  "$external/vstl",
]

source_set("storm_lib") {
  sources = [
    "include/storm/impl/basic_log_sink.cpp",
    "include/storm/impl/basic_logger.cpp",
    "include/storm/sinks/impl/basic_file_sink.cpp",
    "include/storm/sinks/impl/basic_stdout_sink.cpp",
  ]

  public_configs = [ ":storm_config" ]

  public_deps = _storm_deps
}

group("storm") {
  if (storm_header_only) {
    public_configs = [ ":storm_config" ]
    public_deps = _storm_deps
  } else {
    public_deps = [
      ":storm_lib",
    ]
  }
}

source_set("check_handler_stderr_abort") {
  sources = [
    "src/check_handler_stderr_abort.cpp",
  ]
  deps = [
    ":storm",
  ]
}
